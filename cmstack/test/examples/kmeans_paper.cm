spring read_data(output float x[n], param str path, param str type="csv"){
    index i[0:n-1];
    lines = fread(path, type, n);
    x[i] = float(lines[i]);
}
component assignment(input float x[m], state float centroids[centers][m], output int cluster_assignment){
    index i[0:centers-1], j[0:m-1];
    distances[i] = sum[j]((x[j] - centroids[i][j])^2.0)^(0.5);
    cluster_assignment = argmin[i](distances[i]);
}
component update_sum(input float x[m], input int cluster_assignment, state float centroids_sum[centers][m], state float cluster_sizes[centers]){
    index i[0:m-1];
    centroids_sum[cluster_assignment][i] = centroids_sum[cluster_assignment][i] + x[i];
    cluster_sizes[cluster_assignment] = cluster_sizes[cluster_assignment] + 1;
}
component kmeans_train(input float x[m], input float centroids[centers][m], output float centroid_sums[centers][m], output float cluster_sizes[centers]){
    // Initialize cluster sizes and centroid sums
    cluster_sizes = 0;
    centroid_sums = 0;
    // Create output flow for writing to
    flow int cluster_assignment[1];
  	assignment(x, centroids, cluster_assignment);
  	update_sum(x, cluster_assignment, centroid_sums, cluster_sizes);
}
component update_centroids(input float centroid_sums[centers][m], input float cluster_sizes[centers], output float new_centroids[centers][m]){
    index i[0:centers-1], j[0:m-1];
    new_centroids[i][j] = centroid_sums[i][j]/cluster_sizes[i];
}
reservoir store_data(input float centroids[n][m], input int epochs, param str path, param str type="csv"){
    fwrite(centroids, path, type);
}
component main(){
    flow float train_data[784], centroids[10][784], new_centroids[10][784], centroid_sums[10][784], cluster_sizes[10];
    index i[0:9], j[0:783];
    // Naively initialize centroids
    centroids[i][j] = 0;
    // Read training data
    read_data(train_data, "data/train.csv");
    kmeans_train(train_data, centroids, centroid_sums, cluster_sizes);
    update_centroids(centroid_sums, cluster_sizes, new_centroids);
    store_data(new_centroids,5, "data/model.csv");
}
