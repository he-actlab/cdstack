


component avg_pool2d(input float in[n][ic][h][w], output float out[n][ic][oh][ow], param int strides=2, param int pool_size=2, param int padding=0){

    index b[0:n-1], c[0:ic-1], y[0:oh-1], x[0:ow-1];
    index m[0:pool_size-1], n[0:pool_size-1];
    index y_pad[padding:oh + padding -1], x_pad[padding:ow + padding -1];

    padded_input[b][c][y][x] = 0;
    padded_input[b][c][y_pad][x_pad] = x[b][c][y_pad - padding][x_pad - padding];
    out[b][c][y][x] = (1/(pool_size^2))*sum[m][n](in[b][c][strides*y + m][strides*x + n]);

}

component max_pool2d(input float in[n][ic][h][w], output float out[n][ic][oh][ow], param int strides=2, param int pool_size=2, param int padding=0){

    index b[0:n-1], c[0:ic-1], y[0:oh-1], x[0:ow-1];
    index m[0:pool_size-1], n[0:pool_size-1];
    index y_pad[padding:oh + padding -1], x_pad[padding:ow + padding -1];

    padded_input[b][c][y][x] = 0;
    padded_input[b][c][y_pad][x_pad] = x[b][c][y_pad - padding][x_pad - padding];
    out[b][c][y][x] = max[m][n](in[b][c][strides*y + m][strides*x + n]);
}

component batch_flatten(input float in[k][m][n][p], output float out[k][l]) {
    index i[0:l-1], j[0:k-1];
    out[j][i] = in[ i%m ][floor(i/m)%n][floor(i/(m%n))];
}
component dense(input float in[n][m],
                        state float weights[m][p],
                        output float out[n][p]){
    flow float a[n][p];

    index i[0:n-1], j[0:p-1], k[0:m-1];
    out[i][j] = sum[k](in[i][k]*weights[k][j]);
}
component sigmoid(input float in[n][m], output float out[n][m]){
  index i[0:n-1], j[0:m-1];

  out[i][j] = 1.0 / (1.0 + e()^(-in[i][j]));
}

component relu(input float in[n][m], output float out[n][m]) {
    index i[0:n-1], j[0:m-1];
    out[i][j] = in[i][j] > 0 ? in[i][j]: 0.0;

}

component leaky_relu(input float in[n][m], output float out[n][m], param float alpha=0.1) {
    index i[0:n-1], j[0:m-1];
    out[i][j] = in[i][j] > 0 ? in[i][j] : in[i][j]*alpha;
}
component softmax(input float y[n][m], output float out[m]){
    index i[0:m-1], j[0:m-1];

    out[i] = e()^(y[0][i])/sum[j](e()^(y[0][j]));

}
 reservoir store_model(input float model[m], param str type="csv", param str model_path="model.txt"){

     fwrite(model, model_path, type);
 }

component conv2d(input float x[n][h][w][ic], state float kernels[oc][kh][kw][ic], state float bias[ic],
                        output float result[n][oc][oh][ow],param int padding=0, param int strides=1){

        //Compute padding needed for the image
    index b[0:n-1], c[0:oc-1], y[0:oh-1], x[0:ow-1];
    index dy[0:kh-1], dx[0:kw-1], k[0:ic-1];
    index y_pad[padding:oh + padding -1], x_pad[padding:ow + padding -1];

    padded_input[b][k][y][x] = 0;
    padded_input[b][k][y_pad][x_pad] = x[b][c][y_pad - padding][x_pad - padding];

    result[b][c][y][x] = sum[dy][dx][ic](padded_input[b][k][strides*x + dx][strides*y + dy]*kernels[c][k][dy][dx]);
}

component batch_norm(input float data[n][ic][h][w],
                     state float moving_mean[ic], state float moving_var[ic],
                     output float out[n][ic][h][w], state float gamma[ic],
                     state float beta[ic], param bool scale=1, param float epsilon=0.00002){

    index i[0:n-1], j[0:ic-1], y[0:h-1], x[0:w-1];
    momentum = 0.2;
    data_mean[j] = sum[i][y][x](data[i][j][y][x]) / (n*y*x);
    data_var[j] = sum[i][y][x]((data[i][j][y][x] - data_mean[j])^2) / (n*y*x - 1);
    out[i][j][y][x] = ((data[i][j][y][x] - data_mean[j])/(data_var[j] + epsilon)^(0.5))*gamma[j] + beta[j];
    moving_mean[j] = moving_mean[j] * momentum  + data_mean[j]*(1-momentum);
    moving_var[j] = moving_var[j] * momentum  + data_var[j]*(1-momentum);
}




component add_elementwise(input float op1[n][ic][h][w], input float op2[n][ic][h][w], output float out[n][ic][h][w]) {

        index i[0:n-1], j[0:ic-1], y[0:h-1], x[0:w-1];
        out[i][j][y][x] = op1[i][j][y][x] + op2[i][j][y][x];
}

component global_pool(input float data[n][ic][h][w], output float out[n][ic][h][w]){
        index i[0:n-1], j[0:ic-1], y[0:h-1], x[0:w-1];
        out[i][j][0][0] = 1/(h*w) * sum[y][x](data[i][j][y][x]);

}
component add_bias(input float in[n][ic][h][w], state float bias[ic], output float out[n][ic][w][h]){
    index x[0:w-1],y[0:h-1], i[0:n-1], j[0:ic-1];

    out[i][j][y][x] = in[i][j][y][x] + bias[j];

}


component yolo_layer(input float data[n][h][w][ic], output float data_out[n][oh][ow][oc], param int strides=2, param int padding=1, param int mp_pad=0, param int kernel_size=3){


    // Conv edges
    flow float conv1_weight[oc][kernel_size][kernel_size][ic], conv1_out[n][oh][ow][oc];
    // Bias
    flow float c1_bias[oc];

    // Batch Norm
    flow float bn1_out[n][oh][ow][oc], bn1_moving_mean[oc],bn1_moving_var[oc];
    //Activation
    flow float a1[n][h][w][oc];



    conv2d(data, conv1_weight, c1_bias,conv1_out, padding, 1);
    batch_norm(conv1_out, bn1_moving_mean, bn1_moving_var, bn1_out);
    leaky_relu(bn1_out, a1);
    max_pool2d(a1,data_out,strides, 2, mp_pad);

}

component yolo_layer_no_pool(input float data[n][h][w][ic], output float data_out[n][oh][ow][oc], param int strides=1, param int padding=1, param int kernel_size=3){


    // Conv edges
    flow float conv1_weight[oc][kernel_size][kernel_size][ic], conv1_out[n][h][w][oc];
    // Bias
    flow float c1_bias[oc];

    // Batch Norm
    flow float bn1_out[n][h][w][oc], bn1_moving_mean[oc],bn1_moving_var[oc];
    //Activation
    flow float a1[n][h][w][oc];



    conv2d(data, conv1_weight, c1_bias,conv1_out, padding, 1);
    batch_norm(conv1_out, bn1_moving_mean, bn1_moving_var, bn1_out);
    leaky_relu(bn1_out, data_out);

}

component main(){

    // Yolo 1
    flow float data[1][416][416][3], yolo1_out[1][208][208][16], yolo2_out[1][104][104][32], yolo3_out[1][52][52][64];
    flow float yolo4_out[1][26][26][128], yolo5_out[1][13][13][256], yolo6_out[1][13][13][512];

    // Yolo 2
    flow float yolo7_out[1][13][13][1024], yolo8_out[1][13][13][1024];

    // Final layer
    flow float conv0_weight[125][1][1][1024], conv0_out[1][13][13][125], b0_bias[125];
     yolo_layer(data, yolo1_out);
     yolo_layer(yolo1_out, yolo2_out);
     yolo_layer(yolo2_out, yolo3_out);
     yolo_layer(yolo3_out, yolo4_out);
     yolo_layer(yolo4_out, yolo5_out);
     yolo_layer(yolo5_out, yolo6_out, 1,1,1);
     yolo_layer_no_pool(yolo6_out, yolo7_out);
     yolo_layer_no_pool(yolo7_out, yolo8_out);
     conv2d(yolo8_out, conv0_weight, b0_bias, conv0_out, 0);
}